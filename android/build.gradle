buildscript {
  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:8.8.0"
    classpath "org.jetbrains.kotlin:kotlin-serialization:2.2.21"
  }
}

def reactNativeArchitectures() {
  def value = rootProject.getProperties().get("reactNativeArchitectures")
  return value ? value.split(",") : ["armeabi-v7a", "x86", "x86_64", "arm64-v8a"]
}

def isNewArchitectureEnabled() {
  return rootProject.hasProperty("newArchEnabled") && rootProject.getProperty("newArchEnabled") == "true"
}

plugins {
  id "com.ncorti.ktfmt.gradle" version "0.24.0"
}

apply plugin: "com.android.library"
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'org.jetbrains.kotlin.plugin.serialization'
apply from: '../nitrogen/generated/android/AudioBrowser+autolinking.gradle'
apply from: "./fix-prefab.gradle"

if (isNewArchitectureEnabled()) {
  apply plugin: "com.facebook.react"
}

ktfmt {  googleStyle()
}

tasks.register("format") {
  dependsOn "ktfmtFormat"
}

def getExtOrDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties["AudioBrowser_" + name]
}

def getExtOrIntegerDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.properties["AudioBrowser_" + name]).toInteger()
}

def kotlin_version = getExtOrDefault("kotlinVersion") ?: "1.9.0"

android {
  namespace "com.audiobrowser"

  ndkVersion getExtOrDefault("ndkVersion")
  compileSdkVersion getExtOrIntegerDefault("compileSdkVersion")

  defaultConfig {
    minSdkVersion getExtOrIntegerDefault("minSdkVersion")
    targetSdkVersion getExtOrIntegerDefault("targetSdkVersion")
    buildConfigField "boolean", "IS_NEW_ARCHITECTURE_ENABLED", isNewArchitectureEnabled().toString()

    externalNativeBuild {
      cmake {
        cppFlags "-frtti -fexceptions -Wall -Wextra -fstack-protector-all"
        arguments "-DANDROID_STL=c++_shared", "-DANDROID_SUPPORT_FLEXIBLE_PAGE_SIZES=ON"
        abiFilters (*reactNativeArchitectures())

        buildTypes {
          debug {
            cppFlags "-O1 -g"
          }
          release {
            cppFlags "-O2"
          }
        }
      }
    }
  }

  externalNativeBuild {
    cmake {
      path "CMakeLists.txt"
    }
  }

  packagingOptions {
    excludes = [
        "META-INF",
        "META-INF/**",
        "**/libc++_shared.so",
        "**/libfbjni.so",
        "**/libjsi.so",
        "**/libfolly_json.so",
        "**/libfolly_runtime.so",
        "**/libglog.so",
        "**/libhermes.so",
        "**/libhermes-executor-debug.so",
        "**/libhermes_executor.so",
        "**/libreactnative.so",
        "**/libreactnativejni.so",
        "**/libturbomodulejsijni.so",
        "**/libreact_nativemodule_core.so",
        "**/libjscexecutor.so"
    ]
  }

  buildFeatures {
    buildConfig true
    prefab true
  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }

  lintOptions {
    disable "GradleCompatible"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  sourceSets {
    main {
      if (isNewArchitectureEnabled()) {
        java.srcDirs += [
          // React Codegen files
          "${project.buildDir}/generated/source/codegen/java"
        ]
      }
    }
  }
}

repositories {
  mavenCentral()
  google()
}


dependencies {
  // For < 0.71, this will be from the local maven repo
  // For > 0.71, this will be replaced by `com.facebook.react:react-android:$version` by react gradle plugin
  //noinspection GradleDynamicVersion
  implementation "com.facebook.react:react-native:+"

  // Add a dependency on NitroModules
  implementation project(":react-native-nitro-modules")

  // Kotlin stdlib
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

  // Make sure we're using androidx
  implementation "androidx.core:core-ktx:1.13.1"
  implementation "androidx.localbroadcastmanager:localbroadcastmanager:1.1.0"
  implementation "androidx.lifecycle:lifecycle-process:2.8.6"
  implementation 'androidx.media:media:1.7.0'

  implementation("androidx.lifecycle:lifecycle-runtime-ktx:2.8.6")
  implementation("androidx.activity:activity-compose:1.9.3")
  implementation("androidx.compose.ui:ui")
  implementation("androidx.compose.ui:ui-graphics")

  // Media3 dependencies for audio playback.
  // Version is configurable via rootProject.ext.media3Version or AudioBrowser_media3Version
  // in gradle.properties, allowing consuming apps to align with other media3 consumers
  // (e.g. react-native-video).
  def media3Version = getExtOrDefault("media3Version")
  implementation("androidx.media3:media3-exoplayer:${media3Version}")
  implementation("androidx.media3:media3-session:${media3Version}")
  implementation("androidx.media3:media3-ui:${media3Version}")
  implementation("androidx.media3:media3-exoplayer-hls:${media3Version}")
  implementation("androidx.media3:media3-exoplayer-dash:${media3Version}")
  implementation("androidx.media3:media3-exoplayer-smoothstreaming:${media3Version}")
  implementation("androidx.media3:media3-common:${media3Version}")

  implementation "com.google.guava:guava:33.0.0-android"
  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-guava:1.8.0"
  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.3"

  // HTTP client for browser API requests
  implementation 'com.squareup.okhttp3:okhttp:5.3.0'
  implementation 'com.squareup.okhttp3:logging-interceptor:5.3.0'

  // JSON parsing
  implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.9.0'

  // Image loading with Coil (for authenticated artwork, SVG support)
  implementation("io.coil-kt.coil3:coil:3.3.0")
  implementation("io.coil-kt.coil3:coil-svg:3.3.0")
  implementation("io.coil-kt.coil3:coil-network-okhttp:3.3.0")

  // Testing dependencies
  testImplementation 'junit:junit:4.13.2'
  testImplementation 'org.mockito:mockito-core:5.8.0'
  testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.7.3'
  testImplementation 'org.robolectric:robolectric:4.11.1'

  api 'com.jakewharton.timber:timber:5.0.1'
}

if (isNewArchitectureEnabled()) {
  react {
    jsRootDir = file("../src/")
    libraryName = "AudioBrowser"
    codegenJavaPackageName = "com.audiobrowser"
  }
}